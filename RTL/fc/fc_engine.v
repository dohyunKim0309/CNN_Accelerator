`timescale 1ns / 1ps
//////////////////////////////////////////////////////////////////////////////////
// Module Name: fc_engine
// Description:
//   FC layer engine for channel-major packed input.
//
//   Handshake (conv2 패턴 정합):
//     prior_wdone : maxpool 의 wdone direct wire (입력 image 준비 알림)
//     rdone       : FC 가 poolfc bank read 완료 1-cycle pulse
//     input_bank_sel : 내부 toggle FF, rdone 시 토글 (ping-pong)
//     start       : legacy system arm pulse (init 용 backup, 이후 handshake 자동)
//   FC 는 terminal layer 이므로 output 측 handshake (succ_rdone, wdone, output_bank_sel)
//   없음. argmax 결과는 class_idx / class_valid 로 직출.
//
//   Input BRAM (poolfc):
//     width = 128-bit = 16ch * 8-bit
//     depth = 512 = 2 bank * 256 (144 만 유효, 나머지 padding)
//     addr  = {input_bank_sel, s_cnt[7:0]}   — maxpool 의 write 포맷과 일치
//     one address contains all 16 channels for the same spatial position
//
//   Weight BRAM:
//     width = 256-bit
//     depth = 720 = 5 output-pairs * 144 spatial
//     addr = pair_cnt * 144 + s_cnt
//     [127:0]   : even output column weights, 16ch
//     [255:128] : odd  output column weights, 16ch
//
//   BRAM read latency:
//     poolfc (input) BRAM = 2 cycle (L=2, 200MHz)
//     weight BRAM         = 2 cycle (L=2, Primitive Output Register + REGCEB tie1)
//////////////////////////////////////////////////////////////////////////////////

module fc_engine #(
    parameter ACC_W = 24
)(
    input  wire         clk,
    input  wire         rst,
    input  wire         start,

    //==========================================================================
    // FC weight BRAM Port A  (PS write via 512-bit AXI BRAM Ctrl)
    // 512-bit × 1024 (720 used), addr = pair*144 + spatial.
    //   1 word = 16ch × 32b SIMD-packed A (A=W1*2^17+W0). gen 산출(11520×32b) 을
    //   PS 가 변환 없이 그대로 direct write (16 A/word).
    //==========================================================================
    input  wire         fcw_ena,
    input  wire [63:0]  fcw_wea,     // 512-bit byte-write (AXI WSTRB[63:0])
    input  wire [9:0]   fcw_addra,   // symmetric Port A: 512b × 1024 word addr
    input  wire [511:0] fcw_dina,

    //==========================================================================
    // poolfc buffer read port
    //   128-bit × 512 (2 bank × 256), 2-cycle read latency (L=2, bram_pool_to_fc).
    //   addr = {input_bank_sel, s_cnt[7:0]} — bank=0: 0..143, bank=1: 256..399.
    //==========================================================================
    output wire         poolfc_re,
    output wire [8:0]   poolfc_addr,         // {input_bank_sel, s_cnt[7:0]}
    input  wire [127:0] poolfc_dout,

    //==========================================================================
    // Handshake for ping-pong buffer
    //==========================================================================
    input  wire         prior_wdone,
    output wire         rdone,

    //==========================================================================
    // Result
    //==========================================================================
    output wire [3:0]   class_idx,
    output wire         class_valid
);

    //==========================================================================
    // 1. FSM
    //==========================================================================
    wire [7:0] fsm_s_cnt;
    wire [2:0] fsm_pair_cnt;
    wire [9:0] fsm_wbase;
    wire       fsm_comp_v;
    wire       fsm_s_first;
    wire       fsm_s_last;
    wire       fsm_busy;
    wire       fsm_input_bank_sel;

    fc_fsm fsm_inst (
        .clk            (clk),
        .rst            (rst),
        .start          (start),
        .prior_wdone    (prior_wdone),
        .rdone          (rdone),
        .input_bank_sel (fsm_input_bank_sel),
        .s_cnt          (fsm_s_cnt),
        .pair_cnt       (fsm_pair_cnt),
        .wbase          (fsm_wbase),
        .comp_v         (fsm_comp_v),
        .s_first        (fsm_s_first),
        .s_last         (fsm_s_last),
        .busy           (fsm_busy)
    );

    //==========================================================================
    // 2. Input BRAM address (poolfc)
    //
    //   Bank format: {bank_sel, s_cnt[7:0]} — 9-bit, top bit = bank.
    //     bank=0 : addr {0, 0}..{0, 143} = 0..143
    //     bank=1 : addr {1, 0}..{1, 143} = 256..399
    //
    //   ★ maxpool 의 poolfc_wr_addr = {output_bank_sel, out_addr[7:0]} 와 동일 포맷.
    //   이전엔 `(bank ? (144 + s_cnt) : s_cnt)` 였는데 (144..287) maxpool 의 write
    //   range (256..399) 와 mismatch → bank 1 read 시 빈 영역에서 0 만 읽음.
    //==========================================================================
    //   ★ 200MHz: read addr/en 을 1-cycle register (weight addr adder critical path 분리).
    //     poolfc·weight 둘 다 동일하게 +1 → BMG dout 도착 T+2 → T+3. 아래 control tap 전부 +1.
    reg        poolfc_re_r;
    reg [8:0]  poolfc_addr_r;
    always @(posedge clk) begin
        if (rst) begin
            poolfc_re_r   <= 1'b0;
            poolfc_addr_r <= 9'd0;
        end else begin
            poolfc_re_r   <= fsm_comp_v;
            poolfc_addr_r <= {fsm_input_bank_sel, fsm_s_cnt};
        end
    end
    assign poolfc_re   = poolfc_re_r;
    assign poolfc_addr = poolfc_addr_r;

    //==========================================================================
    // 3. Weight BRAM, 512-bit x 720, L=2 read latency
    //    (Primitive Output Register ON + REGCEB tie1 → always-follow)
    //    1 word = 16ch × 32b SIMD-A. fc_pe_array 가 lane 별 [ch*32 +:25] 를
    //    pe_cell.packed_w 로 직결 (재조립 없음).
    //==========================================================================
    //   ★ 200MHz: weight read addr = wbase + s_cnt (CARRY4 가산) → BRAM ADDRBWRADDR 가
    //     워스트 경로(WNS -0.028, net 60%)였음. 1-cycle register 로 가산기를 BRAM setup 에서 분리.
    //     enb 도 같이 register → poolfc 와 동일 +1 정렬 (위 read addr register / 아래 control tap +1).
    reg        fcw_enb_r;
    reg [9:0]  fcw_addrb_r;
    always @(posedge clk) begin
        if (rst) begin
            fcw_enb_r   <= 1'b0;
            fcw_addrb_r <= 10'd0;
        end else begin
            fcw_enb_r   <= fsm_comp_v;
            fcw_addrb_r <= fsm_wbase + {2'd0, fsm_s_cnt};
        end
    end
    wire [511:0] fcw_doutb;

    fc_weight_bram fcw_bmg_inst (
        .clka   (clk),
        .ena    (fcw_ena),                 // ENA=fcw_ena
        .wea    (fcw_wea),                 // WEA=fcw_wea[63:0] byte-write (AXI WSTRB 직결)
        .addra  (fcw_addra),
        .dina   (fcw_dina),

        .clkb   (clk),
        .enb    (fcw_enb_r),
        .addrb  (fcw_addrb_r),
        .doutb  (fcw_doutb),
        .regceb (1'b1)                     // 출력 reg always-follow (마지막 weight sp143 전파)
    );

    //   weight BRAM 을 poolfc 와 동일하게 L=2 (BMG output primitive register) 로 두어
    //   weight(fcw_doutb) 와 x(poolfc_dout) 가 둘 다 T+2 에 도착하도록 정렬한다.
    //   (구: fc_weight_bram L=1 + fabric reg fcw_doutb_r 로 +1 했으나, IP output reg 로
    //    통일 — conv weight BMG 와 동일 방식. REGCEB=1 로 마지막 weight(pair4 sp143) propagation
    //    보장. abrupt-stop(comp_v drop) 에서 REGCEB 미노출이면 ENB-gated → 누락; 그래서 tie1.)
    //   L=2 output register 는 200MHz weight read 타이밍도 닫는다 (clock-to-out ~0.45ns).

    // fcw_doutb = 16ch × 32b SIMD-A (gen 그대로). fc_pe_array 가 lane 별
    // [ch*32 +: 25] 를 pe_cell.packed_w 로 직결 (구: even/odd 분리+재조립 제거).

    //==========================================================================
    // 4. Valid/control alignment
    //
    // PE 는 공용 core/pe_cell (DSP 3-stage + 출력 reg = 4-cycle latency).
    // Timeline for an issued spatial word at cycle T
    //   (poolfc L=2 & weight L=2: 둘 다 BMG output primitive register → T+2 도착):
    //   T+2 : x(poolfc_dout) & weight(fcw_doutb) valid — PE inputs valid (combinational)
    //   T+3 : DSP A/B latch              — pe_en @ T+2 = 1 필요
    //   T+4 : DSP M latch                — pe_en @ T+3 = 1 필요
    //   T+5 : DSP P latch                — pe_en @ T+4 = 1 필요
    //   T+6 : PE 출력 reg (mul0/mul1)    — pe_en @ T+5 = 1 필요
    //   T+7 : adder stage1 reg (e1)      — adder_en @ T+6 = 1 필요
    //   T+8 : adder stage2 reg (e2)
    //   T+9 : adder stage3 reg (e3)
    //   T+10: adder stage4 reg (sum0/1)  — adder_en @ T+9 = 1 필요
    //   T+11: accumulator update         — acc_en @ T+10 = 1 필요
    //
    // comp_pipe[k] @ cycle C = fsm_comp_v @ cycle (C-k-1) (1-cycle 등록 지연부터).
    // poolfc/weight L=2 + read addr/en register(+1) → x & weight 가 issue 기준 T+3 도착
    // (원 L=1 대비 +2). 모든 tap 을 기존 L=2 정렬에서 다시 +1 시프트:
    //   pe_en    = comp_pipe[2] | comp_pipe[3] | comp_pipe[4]
    //                          | comp_pipe[5]                    (covers T+3..T+6)
    //   adder_en = comp_pipe[6] | comp_pipe[7] | comp_pipe[8]
    //                          | comp_pipe[9]                    (covers T+7..T+10)
    //   acc_en/clear/last/pair = *_pipe[10]                      (covers T+11)
    //
    // 주의: accumulator 의 logit 캡처는 "acc + sum" 형태로 마지막 spatial 포함.
    // (RTL/fc/fc_accumulator.v 의 last=1 branch 참조; sp(last) 가 acc0_OLD 에
    //  아직 없을 때도 combinational add 로 logit 에 반영.)
    //==========================================================================
    localparam CTRL_DELAY = 10;  // poolfc L=2(+1) + read addr/en register(+1): 기존 9 +1

    reg [CTRL_DELAY:0] comp_pipe;
    reg [CTRL_DELAY:0] first_pipe;
    reg [CTRL_DELAY:0] last_pipe;
    reg [2:0]          pair_pipe [0:CTRL_DELAY];

    integer k;
    always @(posedge clk) begin
        if (rst) begin
            comp_pipe  <= {(CTRL_DELAY+1){1'b0}};
            first_pipe <= {(CTRL_DELAY+1){1'b0}};
            last_pipe  <= {(CTRL_DELAY+1){1'b0}};
            for (k = 0; k <= CTRL_DELAY; k = k + 1)
                pair_pipe[k] <= 3'd0;
        end else begin
            comp_pipe[0]  <= fsm_comp_v;
            first_pipe[0] <= fsm_s_first;
            last_pipe[0]  <= fsm_s_last;
            pair_pipe[0]  <= fsm_pair_cnt;

            for (k = 1; k <= CTRL_DELAY; k = k + 1) begin
                comp_pipe[k]  <= comp_pipe[k-1];
                first_pipe[k] <= first_pipe[k-1];
                last_pipe[k]  <= last_pipe[k-1];
                pair_pipe[k]  <= pair_pipe[k-1];
            end
        end
    end

    wire pe_en    = comp_pipe[2] | comp_pipe[3] | comp_pipe[4] | comp_pipe[5];
    wire adder_en = comp_pipe[6] | comp_pipe[7] | comp_pipe[8] | comp_pipe[9];

    wire       acc_en    = comp_pipe [CTRL_DELAY];
    wire       acc_clear = first_pipe[CTRL_DELAY];
    wire       acc_last  = last_pipe [CTRL_DELAY];
    wire [2:0] acc_pair  = pair_pipe [CTRL_DELAY];

    //==========================================================================
    // 5. SIMD PE array: 16 lanes, each lane produces even/odd product
    //==========================================================================
    wire [255:0] p_even_flat;
    wire [255:0] p_odd_flat;

    fc_pe_array pe_inst (
        .clk          (clk),
        .rst          (rst),
        .en           (pe_en),
        .x_flat       (poolfc_dout),
        .w_packed_flat(fcw_doutb),        // 512b = 16ch × 32b SIMD-A, lane 별 직결
        .p0_flat      (p_even_flat),
        .p1_flat      (p_odd_flat)
    );

    //==========================================================================
    // 6. 16-channel adder tree for even/odd output columns
    //==========================================================================
    wire signed [19:0] sum_even;
    wire signed [19:0] sum_odd;

    fc_adder_tree adder_inst (
        .clk    (clk),
        .rst    (rst),
        .en     (adder_en),
        .p0_flat(p_even_flat),
        .p1_flat(p_odd_flat),
        .sum0   (sum_even),
        .sum1   (sum_odd)
    );

    //==========================================================================
    // 7. Accumulator
    //==========================================================================
    wire signed [ACC_W-1:0] logit_even_acc;
    wire signed [ACC_W-1:0] logit_odd_acc;
    wire                    logit_valid;

    fc_accumulator #(.ACC_W(ACC_W)) acc_inst (
        .clk        (clk),
        .rst        (rst),
        .en         (acc_en),
        .clear      (acc_clear),
        .last       (acc_last),
        .sum0       (sum_even),
        .sum1       (sum_odd),
        .logit0     (logit_even_acc),
        .logit1     (logit_odd_acc),
        .logit_valid(logit_valid)
    );

    //==========================================================================
    // 8. Logit collection
    //
    // acc_pair = pair_pipe[CTRL_DELAY] 는 logit_valid 가 출력되는 사이클(T+1)에
    // 이미 다음 pair 값으로 시프트되어 있다.
    // acc_last pulse 가 뜨는 사이클(T)의 acc_pair 가 진짜 현재 pair 이므로
    // 그 값을 래치하여 logit_reg 인덱스로 사용한다.
    //==========================================================================
    reg signed [ACC_W-1:0] logit_reg [0:9];
    reg [2:0] acc_pair_latch;

    always @(posedge clk) begin
        if (rst)
            acc_pair_latch <= 3'd0;
        else if (acc_en && acc_last)
            acc_pair_latch <= acc_pair;
    end

    integer oc;
    always @(posedge clk) begin
        if (rst) begin
            for (oc = 0; oc < 10; oc = oc + 1)
                logit_reg[oc] <= {ACC_W{1'b0}};
        end else if (logit_valid) begin
            logit_reg[{1'b0, acc_pair_latch} * 2    ] <= logit_even_acc;
            logit_reg[{1'b0, acc_pair_latch} * 2 + 1] <= logit_odd_acc;
        end
    end

    wire [10*ACC_W-1:0] logit_flat;

    genvar gi;
    generate
        for (gi = 0; gi < 10; gi = gi + 1) begin : flat_pack
            assign logit_flat[gi*ACC_W +: ACC_W] = logit_reg[gi];
        end
    endgenerate

    // Last pair write is visible to argmax on the next cycle.
    reg all_ready;
    always @(posedge clk) begin
        if (rst)
            all_ready <= 1'b0;
        else
            all_ready <= logit_valid && (acc_pair_latch == 3'd4);
    end

    //==========================================================================
    // 9. Argmax
    //==========================================================================
    fc_argmax #(.ACC_W(ACC_W)) argmax_inst (
        .clk       (clk),
        .rst       (rst),
        .in_valid  (all_ready),
        .logit_flat(logit_flat),
        .class_idx (class_idx),
        .done      (class_valid)
    );

endmodule